module bfloat16_chip(
    input  clk,
    input  rst,
    output MISO,
    input  MOSI,
    input  SCLK,
    input  SS
);

reg loadA;
reg loadOP;
reg resetACC;
reg loadACC;
reg loadPISO;
wire [15:0] sipo_reg; 
wire [15:0] piso_reg;
wire [15:0] regA;
wire [15:0] regB;
wire [15:0] acc;
reg  en_addsub;
reg  en_mpy;
wire ready_sipo;
wire ready_addsub;
wire ready_mpy;
wire ready_op;

wire is_sub;
wire is_sum;
wire is_mpy;
wire acc_init0;
wire acc_init1;

wire [15:0] out_addsub;
wire [15:0] out_mpy;

//SPI Interface
//----------------------------
spi_controller u_spi (
    .sclk(SCLK),
    .ss(SS),
    .mosi(MOSI),
    .miso(MISO),
    .load_tx(loadPISO),
    .data_tx(acc), //Always send the accumulator
    .data_rx(sipo_reg), //Received data
    .ready(ready_sipo) // Set when a word is complete 
);

//Operation register 
//----------------------------

//Instruction decoding
//----------------------------
//Bits 7-4:
// 0000: SUM  
// 1000: SUB 
// 0001: MPY 
//Bts 1-0:
// 00: ZERO: Load ACC with 0.0 
// 01: ONE: Load ACC with 1.0 
// 1X: Don't change ACC 

wire load_op = ready_sipo & loadOP;

register #(.N(1)) u_regOP_sub(
    .clk(clk),
    .rst(rst),
    .load(load_op),
    .d(sipo_reg[15]),
    .q(is_sub)
);

register #(.N(1)) u_regOP_sum(
    .clk(clk),
    .rst(rst),
    .load(load_op),
    .d( sipo_reg[13:12] == 2'b00 ),
    .q(is_sum)
);

register #(.N(1)) u_regOP_mpy(
    .clk(clk),
    .rst(rst),
    .load(load_op),
    .d( sipo_reg[13:12] == 2'b01 ),
    .q(is_mpy)
);

register #(.N(1)) u_regOP_acc_init0(
    .clk(clk),
    .rst(rst),
    .load(load_op),
    .d( sipo_reg[9:8] == 2'b00 ),
    .q( acc_init0 )
);

register #(.N(1)) u_regOP_acc_init1(
    .clk(clk),
    .rst(rst),
    .load(load_op),
    .d( sipo_reg[9:8] == 2'b01 ),
    .q( acc_init1 )
);

assign ready_op = (ready_addsub & en_addsub) | (ready_mpy & en_mpy);

//Operands' registers
//----------------------------
register u_regA(
    .clk(clk),
    .rst(rst),
    .load(ready_sipo & loadA),
    .d(sipo_reg),
    .q(regA)
);

//Accumulator 
//----------------------------
wire [15:0] acc_in;
wire [15:0] acc_in0;

assign acc_in0 =  
    is_sum ? out_addsub :
    is_mpy ? out_mpy :
    16'hx;

assign acc_in = 
    resetACC ? 
        (acc_init1 ? 16'h3F80 : 16'h0000) :
        acc_in0;

register u_acc(
    .clk(clk),
    .rst(rst),
    .load(loadACC),
    .d(acc_in),
    .q(acc)
);

//Bfloat16 adder
//----------------------------
fp16sum_res u_addsub(
    .clk(clk),
    .rst(rst),
    .add_sub(is_sub),
    .en(en_addsub),
    .x1(acc),
    .x2( regA ),
    .y(out_addsub),
    .ready(ready_addsub)
);

//Bfloat16 multiplier
//----------------------------
fpmul u_mpy(
    .clk(clk),
    .rst(rst),
    .en(en_mpy),
    .x1(regA),
    .x2(acc),
    .y(out_mpy),
    .ready(ready_mpy)
);

//FSM for control path
//----------------------------

// Machine states
localparam Idle         = 3'd0; //Idle
localparam WaitOp       = 3'd1; //Wait SPI reception of operation
localparam ResetACC     = 3'd2; //Reset ACC if required
localparam WaitNextData1= 3'd3; //Wait for SPI ready signal to be disabled
localparam WaitData1    = 3'd4; //Wait SPI reception of first operand
localparam WaitComp     = 3'd5; //Wait until main computation is done
localparam LoadAcc      = 3'd6; //Load accumulator with final result
localparam LoadOut      = 3'd7; //Load SPI output register

// State register
reg [2:0] state;
reg [2:0] next_state;

//Update state
always @(posedge clk) begin
    if (rst)
        state <= Idle;
    else
        state <= next_state;
end

// Logic of the next state and outputs
always @(*) begin
    next_state  = state;

    loadA = 1'b0;
    loadOP = 1'b0;
    resetACC = 1'b0;
    loadACC = 1'b0;
    loadPISO = 1'b0;
    en_addsub = 1'b0;
    en_mpy = 1'b0;
    
    case (state)
        Idle: begin
            loadPISO = 1'b1;
            if (!SS) 
                next_state = WaitOp;
        end

        WaitOp: begin
            loadOP = 1'b1;
            if (ready_sipo) 
                next_state = ResetACC;
            else if (SS)
                next_state = Idle; 
        end

        ResetACC: begin
            loadOP = 1'b0;
            resetACC = 1'b1;
            loadACC = acc_init0 | acc_init1;
            next_state = WaitNextData1;
        end

        WaitNextData1: begin
            resetACC = 1'b0;
            loadPISO = 1'b1;
            if (!ready_sipo) 
                next_state = WaitData1;
            else if (SS)
                next_state = Idle;
        end

        WaitData1: begin
            loadA = 1'b1;
            loadPISO = 1'b0;
            if (ready_sipo) begin 
                next_state = WaitComp;
            end
            else if (SS)
                next_state = Idle; 
        end

        WaitComp: begin
            loadA = 1'b0;
            en_addsub = is_sum;
            en_mpy = is_mpy;
            if (ready_op)
                next_state = LoadAcc;
        end

        LoadAcc: begin
            en_addsub = 1'b0;
            en_mpy = 1'b0;
            loadACC = 1'b1;
            next_state = LoadOut;
        end

        LoadOut: begin
            loadACC = 1'b0;
            loadPISO = 1'b1; 
            if (!ready_sipo) 
                next_state = WaitData1;
            else if (SS)
                next_state = Idle;
        end

        default: begin
            next_state = Idle;
        end
    endcase

end

endmodule


//------------------------------------------------------
//Parametrizable register with synchronous rest and load 
//------------------------------------------------------

module register #(parameter N=16) (
    input  wire clk,
    input  wire rst,
    input  wire load,
    input  wire [N-1:0] d,
    output reg  [N-1:0] q
);

always @(posedge clk) begin
    if (rst)
        q <= {N{1'b0}};
    else if (load)
        q <= d;
end

endmodule


//------------------------------------------------------
// SPI interface
// MSB-first, SS is active low
// SCLK low on idle, data sampled on positive edge SCLK  
//------------------------------------------------------

module spi_controller #(
    parameter WIDTH = 16
)(
    input  sclk,       // SPI clock
    input  ss,         // Slave Select (active low)
    input  mosi,       // Serial data input
    output miso,       // Serial data output

    input  load_tx,               // Load register for transmission
    input  [WIDTH-1:0] data_tx,   // Transmitted data
    output [WIDTH-1:0] data_rx,   // Received data
    output ready         // Set when a word is complete 
);


spi_bit_counter #(.WIDTH(WIDTH)) u_bit_counter(
    .sclk(sclk),
    .ss(ss),
    .ready(ready) 
);

spi_sipo #(.WIDTH(WIDTH)) u_sipo(
    .sclk(sclk),
    .ss(ss),
    .mosi(mosi),
    .data_out(data_rx)
);

spi_piso #(.WIDTH(WIDTH)) u_piso(
    .sclk(sclk),
    .ss(ss),
    .miso(miso),
    .load(load_tx),
    .data_in(data_tx)
);

endmodule


//------------------------------------------------------
// Bit counter for SPI interface
//------------------------------------------------------

module spi_bit_counter #(
    parameter WIDTH = 16
)(
    input  sclk,       // SPI clock
    input  ss,         // Slave Select (active low)
    output reg ready   // Set when bit count = WIDTH-1 
);

// bit counter
reg [$clog2(WIDTH)-1:0] bit_count;

always @(posedge sclk or posedge ss) begin
    if (ss) begin
        bit_count <= 0;
        ready <= 1'b0;
    end
    else begin
        // Bit counter
        if (bit_count == WIDTH-1) begin
            bit_count <= 0;
            ready <= 1'b1;
        end
        else begin
            bit_count <= bit_count + 1'b1;
            ready <= 1'b0;
        end
    end
end

endmodule

//------------------------------------------------------
//Serial-Input Parallel Output (SIPO) Register
// for the SPI interface
// MSB-first, SS is active low
// SCLK low on idle, data sampled on positive edge SCLK  
//------------------------------------------------------

module spi_sipo #(
    parameter WIDTH = 16
)(
    input  sclk,       // SPI clock
    input  mosi,       // Serial data input
    input  ss,         // Slave Select (active low)
    output reg [WIDTH-1:0] data_out   // Output shift register
);

// Shift register
always @(posedge sclk) begin
    if (!ss) begin
        // Shift MSB first
        data_out <= { data_out[WIDTH-2:0], mosi };
    end
end

endmodule


//------------------------------------------------------
//Parallel Input Serial-Output (PISO) Register
// for the SPI interface
// MSB-first, SS is active low
// SCLK low on idle, data sampled on positive edge SCLK  
//------------------------------------------------------

module spi_piso #(
    parameter WIDTH = 16
)(
    input  sclk,       // SPI clock
    output miso,       // Serial data output
    input  ss,         // Slave Select (active low)
    input  load,       // Load PISO (active high)
    input [WIDTH-1:0] data_in   // Data to be transmitted
);

// Shift register
reg [WIDTH-1:0] shift_reg;
assign miso = shift_reg[WIDTH-1];
wire tclk = ss | sclk;

always @(negedge tclk) begin
    if (load) begin
        shift_reg <= data_in;
    end else begin
        // Shift MSB first
        if (!ss) begin
            shift_reg <= { shift_reg[WIDTH-2:0], 1'b0 };
        end
    end
end

endmodule